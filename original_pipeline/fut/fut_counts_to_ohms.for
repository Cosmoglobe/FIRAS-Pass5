	Subroutine FUT_Counts_to_Ohms (rkounts_cal, kounts_grt, nr,
	1                              callo, calhi, calres, resis_grt)
C------------------------------------------------------------------------------
C               Conversion of GRT telemetry counts to ohms
C	Written 1985 Sep 23 by Rich Isaacman (Applied Research Corp.)
C	Modified 1985 Oct 28 to account for ADC offset and nonlinearity
C
C  This routine takes the telemetry counts associated with all GRTs and the
C  counts associated with all four onboard calibration resistors and returns
C  the resistance of the GRTs in ohms for a given current range.  There is an
C  implicit assumption that the calibration and GRT readouts are taken in
C  the same current range.
C
C  The resistance is estimated by deriving a linear or parabolic ADC curve
C  (conversion of counts to ohms) from all cal resistors whose digital readouts
C  are in the central range of the ADC.  For telemetry words whose value is in
C  the range 0 --> 16383, this implies that the cal counts should be in the
C  range 2000 < N < 14000. If the TLM words are in the range -8192 --> +8191,
C  then the IF statement should test for the range -6000 < N < +6000.
C
C  The derivation is done via the IMSL least-squares routine LLSQF.  The columns
C  of the coefficient matrix Q are arranged such that when only one cal PRT
C  falls in the middle ADC range then the "fit" yields a simple ratio of
C  ohms per count. When 2 cal GRTs are good, the fit solves a straight line,
C  ax + b.  When 3 are good it solves a parabola, and when all 4 are good it
C  derives a least-squares solution for a parabola.
C
C  Input Parameters:	RKOUNTS_CAL =  4-element real array holding the
C				       TLM counts of the four cal resistors
C				       used for this current range.
C
C			KOUNTS_GRT  =  NR-element array of GRT TLM counts
C
C			NR	    =  Number of GRTs to convert (usually 23)
C
C			ICUR	    =  1 for low-current data (1 uamp)
C				    =  2    high-current data   (16 uamp)
C
C  Output Parameter:	RESIS_GRT   =  Array of the GRT resistances (ohms)
C
C  Internal Arrays:	RESIS_CAL   =  4 x 2 array holding the resistances
C				       of the four cal resistors for each
C				       current range. Data supplied by
C				       Dave Huff and John Sutton (728).
C
C			Q(4,3)	    =  Least-squares coefficient matrix
C
C			RHS	    =  array of "good" cal resistances for fit
C
C			TOL         =  Tolerance for solution type. If zero, the
C				       routine does the full solution.
C
C			COEFFS(3)   =  Linear or parabolic coefficients of fit
C
C			RES(4)      =  Residual vector from fit
C
C			IUSE        =  array of indexes of usable cal PRTs
C
C			S1, S2	    =  scratch arrays for IMSL routine
C------------------------------------------------------------------------------
C	DEC. 4, 1985
C	************
C	Modified by E.Fung for the following:
C	(1) Check for the condition when none of the cal resistors are in range.
C	    in that case return a -1.0 for the GRT resistance(s).
C	(2) Change the criterion of a lower bound of 2000 counts to 62 counts
C	    for accepting a cal resistor to be in range.  This is to done for
C	    the "BIGTEST" data generated by R.Pisarski.
C	(3) Change the order of the resistance arrays of the cal resistors
C	    from descending to ascending order.
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C
C	DEC. 11, 1985
C	*************
C	Modified by E.Fung to use a "database driven" scheme for the
C	cal resistance values as well as the lowest and highest valid
C	counts for the cal resistors, instead of hard-coding this as
C	data statements before this.
C
C	There are 3 new parameters:
C		CALLO(4)	I*2	4-element array containing the
C					lowest valid count.  In most cases
C					this number should be 2000 for positive
C					counts only, -6000 otherwise
C		CALHI(4)	I*2	4-element array containing the
C					highest valid count.  Should be 14000
C					if counts are all positive, 6000 otherwise
C		CALRES(4)	R*4	The actual cal. resistance of the 4
C					cal resistors (each group has 4 cal
C					resistors).
C	Because of this change, it is no longer necessary to use the internal
C	array RESIS_CAL.
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C	   IF (RKALCOUNTS.GT.2000 .AND. RKALCOUNTS.LT.14000) THEN
C	above changed to:
C
C	   if (Rkalcounts.gt.62 .and. Rkalcounts.lt.14000) then
C	1985 Dec 11, above changed to:
C	    IF (RKALCOUNTS.GT.CALLO(J) .AND. RKALCOUNTS.LT.CALHI(J)) THEN
C
C	     RHS(NCAL) = RESIS_CAL(IUSE(NCAL),ICUR)
C	1985 Dec 11, above changed to:
C	     RHS(NCAL) = CALRES(IUSE(NCAL))
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C	1988 May 19
C	S.CHINTALA	STX/COBE/INGEST		19-MAY-88
C	  SPR # 2088 :
C	      Replaced call to old IMSL routine   :-
C	  CALL LLSQF (Q, 4, KAL_OK, 3, RHS, TOL, KBASIS, COEFFS, S1, S2, IER)
C         by the new IMSL routine :-
C	  CALL LSQRR(KAL_OK,3,Q,4,RHS,TOL,COEFFS,RES,KBASIS)
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C	1989 May 22
C	R.KUMMERER	STX/COBE/FIRAS		22-MAY-89
C	  SPR # 3853 :
C	     Perform calculations in REAL*8.  Requires call to different IMSL
C	     routine    :-
C	  CALL LSQRR(KAL_OK,3,Q,4,RHS,TOL,COEFFS,RES,KBASIS)
C         by the new IMSL routine :-
C	  CALL DLSARG(KBASIS,Q,4,RHS,1,COEFFS)
C	1989 May 22, R.KUMMERER
C	CALL LSQRR (	KAL_OK			! NRA
C     +			,3			! NCA
C     +			,Q			! A
C     +			,4			! LDA
C     +			,RHS			! B
C     +			,TOL			! TOL
C     +			,COEFFS			! X
C     +			,RES			! RES
C     +			,KBASIS			! KBASIS
C     +		   )
C	  REPLACED THE ABOVE ROUTINE BY THE REAL*8 IMSL ROUTINE DLSARG
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C	1989 May 22,  R.Kummerer, Replaced the following
C	DIMENSION  KOUNTS_GRT(1), RKOUNTS_CAL(1), RESIS_GRT(1),
C     .			Q(4,3), RHS(4), S1(3), S2(3), COEFFS(3), IUSE(4)
C with:
C	DIMENSION  KOUNTS_GRT(1), RKOUNTS_CAL(1), RESIS_GRT(1),
C     .			S1(3), S2(3), IUSE(4)
C	REAL*8 Q(4,3), RHS(4), COEFFS(3), TOL, RES(4)
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C	DATA RESIS_CAL /19900., 15000., 10000., 1243.75,
C    .			10000., 1243.75,  625.,  312.5/
C
C     Above changed to:
C
C
C	DATA RESIS_CAL /1243.75, 10000., 15000.,   19900.,
C     .			312.5,   625.,   1243.75,  10000./
C     Above DATA statement not needed when we go to "database driven" method
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C       1989 Aug 21
C	S.READ          STX/COBE/FIRAS          21-AUG-89
C	  SPR # 4328 :
C	     Update COUNTS_TO_OHMS to call the correct Real*8 IMSL routine
C	     to perform the conversions.  Replace the sequence:
C	  CALL DLSARG(KBASIS,Q,4,RHS,1,COEFFS)
C	     with the sequence:
C	  CALL DLSQRR(KAL_OK,3,Q,4,RHS,TOL,COEFFS,RES,KBASIS)
C	     and declare TOL and RES as Real*8.
C	CALL DLSARG (	KBASIS			! KBASIS
C    +			,Q			! A
C    +			,4			! LDA
C    +			,RHS			! B
C    +			,1			! IPATH
C    +			,COEFFS			! X
C    +		   )
C
C	1989 Aug 21, S.READ
C	  REPLACED THE ABOVE ROUTINE BY THE REAL*8 IMSL ROUTINE DLSQRR
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C	1991 Sep 25, Harte Wang, STX
C	  Changed "kounts_cal" (I*4) to "rkounts_cal" (R*4)
C - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
C	1991 Oct 03, Fred Shuman, STX
C	  Bring up to CSDR standard--Implicit None; remove "Dimension"-
C	  declarations; move commented-out code up to the comment header.
C------------------------------------------------------------------------------

	Implicit None

C  Calling arguments:

	Real	*4	rkounts_cal(1), calres(4), resis_grt(1)
	Integer	*4	kounts_grt(1), nr
	Integer	*2	callo(4), calhi(4)

C  Other variables:

	Real	*8	q(4,3), rhs(4), coeffs(3), tol, res(4)
	Integer	*4	j, ncal, kal_ok, kbasis, iuse(4)
	Real	*4	rkalcounts, tlm, s1(3), s2(3)

C
C  First determine which cal resistors are useable and put their indices in IUSE
C
	kal_ok = 0
	Do j=1,4
	   rkalcounts = rkounts_cal(j)
	   If (rkalcounts .Gt. callo(j) .And. rkalcounts .Lt. calhi(j)) Then
	      kal_ok = kal_ok + 1
	      iuse(kal_ok) = j
	   End If
	End Do
C
C  Now fill the least-square coefficient matrix with counts from the good PRTs
C  only. Use the number of good PRTs to determine how many columns of the Q
C  to use as bases for the fit (i.e. ratio, linear, or parabolic).
C
	If (kal_ok .Ne. 0) Then

	   Do ncal=1,kal_ok
	      tlm = rkounts_cal(iuse(ncal))
	      q(ncal,1) = tlm
	      q(ncal,2) = 1.
	      q(ncal,3) = tlm * tlm
	      rhs(ncal) = calres(iuse(ncal))
	   End Do
	   kbasis = Min0 (kal_ok, 3)			! Order of fit
	   tol = 0.

	   Call DLSQRR ( kal_ok			! nra
	2		,3			! nca
	3		,q			! a
	4		,4			! lda
	5		,rhs			! b
	6		,tol			! tol
	7		,coeffs			! x
	8		,res			! res
	9		,kbasis			! kbasis
	1		)
C
C  Apply fit to convert all GRT counts to ohms
C
	   Do j=1,nr
	      tlm = kounts_grt(j)
	      resis_grt(j) = Sngl(coeffs(2) + tlm*(coeffs(1)+ tlm*coeffs(3)))
	   End Do
	Else
	   Do j = 1, nr
	      resis_grt(j) = -1.0
	   End Do
	End If		! If (kal_ok .Ne. 0)

	Return

	End
