;-------------------------------------------------------------------------------
;
;+NAME/ONE LINE DESCRIPTION OF ROUTINE:
;    FLA_Init_FIR reads the reference datasets for the FIRAS synthetic line
;    profile generating program FLA_FIR_LINE.
;
; DESCRIPTION:
;    FLA_Init_FIR reads the binary reference datasets FEX_NYQUIST and FEX_APOD.
;    These reference datasets are pointed to by the logical pointer
;    CSDR$FIRAS_REF.  It also reads the ASCII reference datasets
;    CSDR$FIRAS_IN:FEX_GAIN_CCSS.F16_93HYBRID, which are generated by the
;    FORTRAN program FLA_GAIN.
;
; CALLING SEQUENCE:
;    fla_init_fir, mode, solution
;
; ARGUMENTS (I=input):
;    mode     I  string  Instrument channel and scan mode
;    solution I  string  Calibration model solution file name extension
;
;  WARNINGS:
;      The logical pointers CSDR$FIRAS_IN and CSDR$FIRAS_REF must be defined.
;
;  EXAMPLE:
;    To run FLA_INIT_FIR for the RHSS scan mode and the F16_93HYBRID
;    calibration model solution, the invocation would be:
;
;        UIDL> fla_init_fir,'rhss','f16_93hybrid'
;
;#
;
;  Author:  Kevin Turpie
;           General Sciences Corporation
;  Converted to FLA by:  Gene Eplee
;                        General Sciences Corp.
;                        14 September 1993
;                        SER 11413
;
;  Changes:
;
;    Put correct linearized ifg peak positions into PEAKS array.
;    Gene Eplee, GSC, 30 August 1993.
;
;    Put in logical pointers CSDR$FIRAS_IN and CSDR$FIRAS_REF.  Put in
;    calibration model solution filename extension.
;    Gene Eplee, GSC, 14 September 1993.
;
;-
;-------------------------------------------------------------------------------
;
 PRO FLA_Init_FIR, Mode, Solution
;
 On_Error, 2 ; return to caller if an error occurs.
;
 Common FIRAS_Modes,   Modes,      NGroups,     Peaks,   $
                       Channels,   Lengths,     Speeds,  $
                       Nyquists
;
 Common FIRAS_Gain,    GainMode,   Gain,                 $
                       FreqArray,  Bin0,        BinN
;
 Common FIRAS_Apod,    Apod
;
;
;Begin,
    If (N_Elements( Modes ) eq 0) then begin
;
;      Set up main look-up tables :
;
       Modes    = [ 'RHSS', 'RHSF', 'RHLS', 'RHLF', $
                    'RLSS', 'RLSF', 'RLLS', 'RLLF', $
                    'LHSS', 'LHSF', 'LHLS', 'LHLF', $
                    'LLSS', 'LLSF', 'LLLS', 'LLLF' ]
       NGroups  = [     3,      2,      3,      2,  $
                        3,      2,     12,      8,  $
                        3,      2,      3,      2,  $
                        3,      2,     12,      8  ]
       Peaks    = [   354,    355,    352,    352,  $
                      354,    355,     87,     87,  $
                      354,    355,    352,    352,  $
                      354,    355,     87,     87  ]
       Channels = [     0,      0,      0,      0,  $
                        1,      1,      1,      1,  $
                        2,      2,      2,      2,  $
                        3,      3,      3,      3  ]
       Lengths  = [     0,      0,      1,      1,  $
                        0,      0,      1,      1,  $
                        0,      0,      1,      1,  $
                        0,      0,      1,      1  ]
       Speeds   = [     0,      1,      0,      1,  $
                        0,      1,      0,      1,  $
                        0,      1,      0,      1,  $
                        0,      1,      0,      1  ]
;
;      Set up Nyquist frequency look up table :
;
       File = 'CSDR$FIRAS_REF:FEX_NYQUIST.DAT'
       Get_LUN, LUN
       OpenR, LUN, File, Error=ErrStat
;
;      Open Error Exception Handling
;      -----------------------------
       If (ErrStat ne 0) then begin
          Free_LUN, LUN
          Message, "ERROR: Cannot open file " + File
       EndIf
;
       Nyquists = FltArr( 32 )
       ReadU, LUN, Nyquists
;
       Close, LUN
       Free_LUN, LUN
    EndIf
;
;   Input appropriate gain function :
;
    NoOldMode = (N_Elements( GainMode ) eq 0)
    NoNewMode = (N_Elements( Mode )     eq 0)
;
    If (NoOldMode) then GainMode = ""
    If (NoNewMode) then Mode     = ""
;
    ChangeMode = (GainMode ne StrLowcase( Mode ))
;
    If (ChangeMode and not NoNewMode) then begin
;
;      Load the gain function and save the frequency array and the
;      start and stop indices (range of the full 256 point spectrum):
;
       Ptr       = Where( StrUpcase( Mode ) eq Modes )
       ModePtr   = Ptr(0)
       If (ModePtr lt 0) then Message, "ERROR: "+Mode+" is not a valid mode."
;
       File = 'CSDR$FIRAS_IN:FEX_GAIN_' + StrTrim( StrUpcase( Mode ), 2 ) + $
              '.' + StrUpcase( Solution ) 
       Get_LUN, LUN
       OpenR, LUN, File, Error=ErrStat
;
;      Open Error Exception Handling
;      -----------------------------
       If (ErrStat ne 0) then begin
          Free_LUN, LUN
          Message, "ERROR: Cannot open file " + File
       EndIf
;
;      File Input Loop
;      ---------------
       i = 0 & Title = ' '
       ReadF, LUN, Title, Format = '(A)'
;
       BinFrq = 0. & Real = 0. & Imag = 0.
;
       While (not EoF( LUN )) do begin
          ReadF, LUN, BinFreq, Real, Imag,                             $
                      Format = '(2X,F10.7,2X,E20.13,2X,E20.13)'
          If (i gt 0) then FreqArray = [ FreqArray, BinFreq ]          $
                      else FreqArray = [ BinFreq ]
          If (i gt 0) then Gain      = [ Gain, Complex( Real, Imag ) ] $
                      else Gain      = [ Complex( Real, Imag ) ]
          i = i + 1
       EndWhile
;
       N = N_Elements( FreqArray )-1
;
       indx      = ModePtr mod 8
       Bin0      = Fix( 256.0 * FreqArray(0) / Nyquists(indx) + .5 )
       BinN      = Fix( 256.0 * FreqArray(N) / Nyquists(indx) + .5 )
;
       GainMode  = StrLowcase( Mode )
       Close, LUN
       Free_LUN, LUN
;
;      Open the apodization file and read the record :
;
       File = 'CSDR$FIRAS_REF:FEX_APOD.DAT'
       OpenR, LUN, File, Error=ErrStat
;
;      Open Error Exception Handling
;      -----------------------------
       If (ErrStat ne 0) then begin
          Free_LUN, LUN
          Message, "ERROR: Cannot open file " + File
       EndIf
;
       Buff      = Assoc( LUN, DblArr( 512 ) )
       RecNo     = FLA_Apod_Rec( Mode )
       Apod      = Buff( RecNo )
;
       Close, LUN
       Free_LUN, LUN
    EndIf
;
    Return
 End
