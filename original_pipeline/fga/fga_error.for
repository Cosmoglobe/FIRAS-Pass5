	Integer*4 Function FGA_Error ( sigargs, mechargs )

C------------------------------------------------------------------------
C    PURPOSE: Intercept error messages to screen; suppress as wanted.
C             Initially, only "Output Conversion Error" (generated by WRITE
C             statement that overflows a field allotment in the FORMAT) is 
C             captured, and it is issued to the user only on first occurrence.
C             SPR 2552.
C
C       REQUIREMENTS REFERENCE NUMBERS:
C
C       INPUT DATA:
C
C       OUTPUT DATA:
C
C    AUTHOR: Fred Shuman
C            STI Incorporated
C            January 4, 1989
C
C	     Adapted from FUT_Error by Rob Kummerer
C                         from work performed by E.Cheng.
C
C    INVOCATION: Call LIB$Establish ( FGA_ERROR )
C
C    INPUT PARAMETERS:
C      SIGARGS(*)		I*4	Signal array
C      MECHARGS(*)		I*4	Mechanism array
C
C    OUTPUT PARAMETERS: None
C
C    SUBROUTINES CALLED:  LIB$SIGNAL
C
C    COMMON VARIABLES USED:
C      Error_Count             I*2      Number of errors recorded.
C
C    INCLUDE FILES:  FGA_Error.txt
C
C----------------------------------------------------------------------
C
C Changes:
C
C----------------------------------------------------------------------

	Implicit	None

	Include 	'($SSDEF)'
	Include 	'($FORDEF)'
	Include 	'($STSDEF)'
	Include		'(FGA_Error)'

	Integer		*4	sigargs(*)		!Signal array
	Integer		*4	mechargs(*)		!Mechanism array

	Integer		*4	imatch
	Integer		*4	error_max

	Parameter		(error_max = 1)

	Integer		*4	LIB$Match_Cond

C
C Determine whether this is an error of interest
C
	IMATCH = LIB$MATCH_COND(SIGARGS(2),FOR$_OUTCONERR)
C
C This error is not captured here; pass it on along.
C
	If (IMATCH .eq. 0) Then
	   FGA_Error = SS$_Resignal
C
C Output Conversion Errors, FOR$_OUTCONERR (from FORTRAN Write statements)--
C    allow the first one, and ignore the rest.
C
	Else If (IMATCH .eq. 1) Then
	   If (error_count .Ge. error_max) Then
	      FGA_Error = SS$_Continue
	   Else
	      FGA_Error = SS$_Resignal
	   End If

	End If
C
C Count the error.
C
	error_count = error_count + 1
C
C Done
C
	Return
	End
