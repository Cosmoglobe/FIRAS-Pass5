
!  Command Language Definition file for facility FSL_SPECTRA_LONG

	DEFINE VERB FSL_SPECTRA_LONG


		IMAGE "CSDR$SYSTEM:FSL.EXE"


			QUALIFIER FILE_EXT,		!  Input File Extension
				VALUE(TYPE=$FILE,REQUIRED),            
                                NEGATABLE

			QUALIFIER JSTART,		!  Start Timetag
				VALUE(REQUIRED),
				NONNEGATABLE

			QUALIFIER JSTOP,		!  Stop Timetag
				VALUE(REQUIRED),
				NONNEGATABLE

			QUALIFIER INPUT,		!  Input Data Type
				VALUE(TYPE=FSL_INPUT,REQUIRED),
				NONNEGATABLE

			QUALIFIER PIXEL			!  Input Pixel List
				VALUE(TYPE=$NUMBER,LIST,REQUIRED),
				NONNEGATABLE

			QUALIFIER QUALITY		!  Data Quality
				VALUE(TYPE=FSL_QUALITY,LIST),
				DEFAULT,
				NONNEGATABLE

			QUALIFIER CHANNEL,		!  Channel Specifier
				VALUE(TYPE=FSL_CHAN,REQUIRED),
				NONNEGATABLE

			QUALIFIER SCAN_MODE,		!  Scan Mode Specifier
				VALUE(TYPE=FSL_SCAN,REQUIRED),
				NONNEGATABLE

			QUALIFIER SINGLE_IFG,		!  Process Single IFGs
				NEGATABLE

			QUALIFIER CALIBRATE,		!  Calibrate Spectra
				DEFAULT,
				NEGATABLE

			QUALIFIER MODEL_EXT,		!  Model File Extension
				VALUE(TYPE=$FILE,REQUIRED),
				NEGATABLE

			QUALIFIER DVECTOR,		!  Use D-Vector File
				NEGATABLE

			QUALIFIER FIL_VAR,		!  Use all-sky variances
				NEGATABLE               !  from FIL

			QUALIFIER TSIG_ZERO,		!  Set temperature 
				NEGATABLE               !  sigmas to zero

			QUALIFIER DIFFERENTIAL,		!  Compute Diff Spectra
				NEGATABLE

			QUALIFIER PLOT_DEVICE,		!  Plot Model, Spectra
				VALUE,
				NEGATABLE

			QUALIFIER PLT_FILE,		!  PLT Command File
				VALUE(TYPE=$FILE,DEFAULT=PLT.PCO),
				NEGATABLE

			QUALIFIER WRITE,		!  Write Spectra
				VALUE(TYPE=FSL_WRITE,REQUIRED),
				NEGATABLE

			QUALIFIER REPORT,		!  Processing Report
				VALUE(TYPE=$FILE),
				DEFAULT,
				NEGATABLE


			DISALLOW FILE_EXT AND (JSTART OR JSTOP)
			DISALLOW SCAN_MODE.FL AND (CHANNEL.RH OR CHANNEL.LH)
			DISALLOW SCAN_MODE.FS AND (CHANNEL.RH OR CHANNEL.LH)
			DISALLOW PIXEL AND INPUT.CAL
			DISALLOW NEG CALIBRATE AND (WRITE.CAL OR WRITE.BOTH)
			DISALLOW NEG CALIBRATE AND MODEL_EXT
			DISALLOW CALIBRATE AND NEG MODEL_EXT
			DISALLOW NEG CALIBRATE AND DVECTOR
			DISALLOW NEG CALIBRATE AND FIL_VAR
			DISALLOW DVECTOR AND FIL_VAR
			DISALLOW NEG CALIBRATE AND DIFFERENTIAL


			DEFINE TYPE FSL_INPUT
				KEYWORD SKY,
				KEYWORD CAL

			DEFINE TYPE FSL_QUALITY
				KEYWORD INSTRUMENT,
					VALUE(TYPE=$NUMBER,DEFAULT=3),
					DEFAULT
				KEYWORD ATTITUDE,
					VALUE(TYPE=$NUMBER,DEFAULT=3),
					DEFAULT

			DEFINE TYPE FSL_CHAN
				KEYWORD RH,
				KEYWORD RL,
				KEYWORD LH,
				KEYWORD LL

			DEFINE TYPE FSL_SCAN
				KEYWORD SS,
				KEYWORD SF,
				KEYWORD LF,
                                KEYWORD FS,
				KEYWORD FL

			DEFINE TYPE FSL_WRITE
				KEYWORD VOLT,
				KEYWORD CAL,
				KEYWORD BOTH
